// Class for the mise-action step
// https://github.com/jdx/mise-action

import "https://cdn.jsdelivr.net/gh/StefMa/pkl-gha@77880b64db1f3bf24b31a359311a6f7ef64bffaa/Workflow.pkl"

class Mise extends Workflow.Step {
  uses = "jdx/mise-action@v2"

  hidden version: String?
  hidden install: Boolean?
  hidden install_args: String?
  hidden cache: Boolean?
  hidden experimental: Boolean?
  hidden logLevel: ("debug"|"info"|"warning"|"error")?
  hidden toolVersions: String?
  hidden miseToml: String?
  hidden workingDirectory: String?
  hidden reshim: Boolean?
  hidden githubToken: String?

  local localWith = new Mapping {
    ["version"] = version
    ["install"] = install
    ["install-args"] = install_args
    ["cache"] = cache
    ["experimental"] = experimental
    ["log-level"] = logLevel
    ["tool-versions"] = toolVersions
    ["mise-toml"] = miseToml
    ["working-directory"] = workingDirectory
    ["reshim"] = reshim
    ["github-token"] = githubToken
  }

  local filteredWith = localWith.toMap().filter((_, val) -> val != null)
  with = if (filteredWith.isEmpty) null else filteredWith.toMapping()
}
